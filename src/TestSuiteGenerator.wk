import TestClass;
import TestMethod;
import File;

#>
# will generate a file which looks like
#
# every TestSuite is:
# 	needs YourFirstClassTest, YourSecondClassTest, YourThirdClassTest, TestResultReporter;
#	provides Asserts;
#	tests() {
#		var Asserts <- this;
#		TestResultReporter.begin("YourFirstClassTest", "testFirstMethod");
#		YourFirstClassTest.testFirstMethod(Asserts);
#		TestResultReporter.end(Asserts);
#		// moar of these
#		TestResultReporter.report();
#	}
##
every TestSuiteGenerator is:

	write(TestClass[])To(File) {
		for(var Num = 0; Num < TestClass.getSize(); Num += 1) {
			File.write("import " + TestClass[Num].getName() + ";");
		}

		File.write("import System; import TestResultReporter; import Asserts; import Printer;");
		File.write("every TestSuite is: needs ");

		for(var Num = 0; Num < TestClass.getSize(); Num += 1) {
			File.write(TestClass[Num].getName());
			File.write(", ");
		}

		File.write("TestResultReporter; provides Asserts, Printer; tests() {");
		File.write("var Asserts <- this;");

		for(var Num = 0; Num < TestClass.getSize(); Num += 1) {
			var TestMethod[] = TestClass[Num].getMethods();

			for(var $Num = 0; $Num < TestMethod.getSize(); $Num += 1) {
				File.write("Asserts = Asserts <- this;");
				File.write("TestResultReporter.begin(\"" + TestClass[Num].getName() + "\", \"" + TestMethod[Num].getName() + "\");");
				File.write(TestClass[Num].getName() + "." + TestMethod[$Num].getName() + ";");
				File.write("TestResultReporter.end(Asserts);");
			}
		}

		File.write("TestResultReporter.report(); }");
	}
