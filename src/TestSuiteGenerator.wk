import TestClass;
import TestMethod;
import File;

#>
# will generate a file which looks like
#
# every TestSuite is:
# 	needs YourFirstClassTest, YourSecondClassTest, YourThirdClassTest, TestResultReporter;
#	provides Asserts;
#	tests() {
#		var Asserts from this;
#		TestResultReporter.begin("YourFirstClassTest", "testFirstMethod");
#		YourFirstClassTest.testFirstMethod(Asserts);
#		TestResultReporter.end(Asserts);
#		// moar of these
#		TestResultReporter.report();
#	}
##
every TestSuiteGenerator is:

	write(TestClass[])To(File) {
		foreach(TestClass[]) {
			File.write("import " + TestClass.getName() + ";");
		}

		File.write("import System; import TestResultReporter; import Asserts; import Printer;");
		File.write("every TestSuite is: needs ");

		foreach(TestClass[]) {
			File.write(TestClass.getName());
			File.write(", ");
		}

		File.write("TestResultReporter; provides Asserts, Printer; tests() {");
		File.write("var Asserts from this;");

		foreach(TestClass[]) {
			foreach(TestClass.getMethods()) {
				File.write("Asserts = Asserts from this;");
				File.write("TestResultReporter.begin(\"" + TestClass.getName() + "\", \"" + TestMethod.getName() + "\");");
				File.write(TestClass.getName() + "." + TestMethod.getName() + ";");
				File.write("TestResultReporter.end(Asserts);");
			}
		}

		File.write("TestResultReporter.report(); }");
	}
